if(NOT BUILD_EXAMPLES)
    return()
endif()

# libmod_cmake
# =============================================================================
set(workDir ${CMAKE_CURRENT_BINARY_DIR}/workDir/libmod_cmake)
file(MAKE_DIRECTORY ${workDir})
add_test(NAME example_libmod_cmake_configure
        COMMAND ${CMAKE_COMMAND} -G "${CMAKE_GENERATOR}"
        -DBOOST_ROOT=${BOOST_ROOT}
        -DCMAKE_PREFIX_PATH=${CMAKE_PREFIX_PATH}
        ${CMAKE_CURRENT_SOURCE_DIR}/libmod_cmake
        WORKING_DIRECTORY ${workDir})
add_test(NAME example_libmod_cmake_build
        COMMAND ${CMAKE_COMMAND} --build .
        WORKING_DIRECTORY ${workDir})
set_tests_properties(example_libmod_cmake_configure PROPERTIES
        FIXTURES_SETUP example_libmod_cmake)
set_tests_properties(example_libmod_cmake_build PROPERTIES
        FIXTURES_REQUIRED example_libmod_cmake)

# pymod_extension
# =============================================================================
if(BUILD_PY_MOD)
    set(workDir ${CMAKE_CURRENT_BINARY_DIR}/workDir/pymod_extension)
    file(MAKE_DIRECTORY ${workDir})
    add_test(NAME example_pymod_extension_configure
            COMMAND ${CMAKE_COMMAND} -G "${CMAKE_GENERATOR}"
            -DBOOST_ROOT=${BOOST_ROOT}
            -DCMAKE_PREFIX_PATH=${CMAKE_PREFIX_PATH}
            ${CMAKE_CURRENT_SOURCE_DIR}/pymod_extension
            WORKING_DIRECTORY ${workDir})
    add_test(NAME example_pymod_extension_build
            COMMAND ${CMAKE_COMMAND} --build .
            WORKING_DIRECTORY ${workDir})
    add_test(NAME example_pymod_extension_run
            COMMAND ${CMAKE_INSTALL_FULL_BINDIR}/mod -f ${CMAKE_CURRENT_SOURCE_DIR}/pymod_extension/test.py
            WORKING_DIRECTORY ${workDir})
    set_tests_properties(example_pymod_extension_configure PROPERTIES
            FIXTURES_SETUP example_pymod_extension)
    set_tests_properties(example_pymod_extension_build PROPERTIES
            FIXTURES_REQUIRED example_pymod_extension)
    set_tests_properties(example_pymod_extension_build PROPERTIES
            FIXTURES_SETUP example_pymod_extension_run)
    set_tests_properties(example_pymod_extension_run PROPERTIES
            FIXTURES_REQUIRED example_pymod_extension_run)
endif()

# py examples
# =============================================================================
if(BUILD_PY_MOD)
    foreach(fileName ${mod_EXAMPLES_PY_FILES})
        string(REPLACE "/" "__" testName "example_${fileName}")
        set(workDir ${CMAKE_CURRENT_BINARY_DIR}/workDir/${testName})
        file(MAKE_DIRECTORY ${workDir})
        add_test(NAME ${testName}
                COMMAND ${CMAKE_INSTALL_FULL_BINDIR}/mod -f ${CMAKE_CURRENT_LIST_DIR}/${fileName}.py
                WORKING_DIRECTORY ${workDir})
        set_tests_properties(${testName} PROPERTIES
                ENVIRONMENT "MOD_NUM_POST_THREADS=1;MOD_NO_DEPRECATED=1")
        add_coverage_case(${testName})
    endforeach()
endif()